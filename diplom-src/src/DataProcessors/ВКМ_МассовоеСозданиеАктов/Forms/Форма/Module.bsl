
&НаСервере
Процедура ЗаполнитьТаблицуНаСервере() 
	
	Если ПроверкаРеквизитов() = Ложь Тогда
		Возврат;
	КонецЕсли;

	Объект.ДоговораИРеализации.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	РеализацияТоваровУслуг.Ссылка КАК Реализация
		|ПОМЕСТИТЬ ВТ_ДоговораСРеализацией
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Договор
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ВТ_ДоговораСРеализацией.Реализация КАК Реализация,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговораСРеализацией КАК ВТ_ДоговораСРеализацией
		|		ПО ДоговорыКонтрагентов.Ссылка = ВТ_ДоговораСРеализацией.Договор
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И ВТ_ДоговораСРеализацией.Реализация.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоговорыКонтрагентов.Ссылка,
		|	ВТ_ДоговораСРеализацией.Реализация,
		|	ДоговорыКонтрагентов.Владелец";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
						
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Объект.ДоговораИРеализации.Добавить();
		НоваяСтрока.Договор = ВыборкаДетальныеЗаписи.Договор;
		НоваяСтрока.Реализация = ВыборкаДетальныеЗаписи.Реализация; 
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю("Таблица заполнена!");
				
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицу(Команда)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры 

&НаСервере
Функция СоздатьРеализацииЗаПериодНаСервере(Период, Организация)
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьРеализацииЗаПериодВФоне", Период, Организация); 
	
	Возврат ДлительнаяОперация;
	
КонецФункции  

&НаКлиенте
Процедура СоздатьРеализацииЗаПериод(Команда) 
	
	Если ПроверкаРеквизитов() = Ложь Тогда
		Возврат;
	КонецЕсли;
		
	Организация = Объект.Организация;
	Период = Объект.Период;

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.Интервал = 1;
	
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);  
	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
	
	ДлительнаяОперация = СоздатьРеализацииЗаПериодНаСервере(Период, Организация);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры  

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт  
	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		Сообщить(ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт  
	
	
	Если Прогресс.Прогресс <> Неопределено Тогда
		
		Состояние("Создание документов: ", Прогресс.Прогресс.Процент);
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Функция ПроверкаРеквизитов() 
	
	Если ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Период) Тогда
		Возврат Истина;
	Иначе            
		ОбщегоНазначения.СообщитьПользователю("Заполните реквизиты документа");
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура БезФона(Команда)
	БезФонаНаСервере();
КонецПроцедуры

&НаСервере
Процедура БезФонаНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(ВКМ_ГрафикиРаботы.Дата, МЕСЯЦ) КАК МесяцВПериоде
		|ПОМЕСТИТЬ ВТ_МесяцаВПериоде
		|ИЗ
		|	РегистрСведений.ВКМ_ГрафикиРаботы КАК ВКМ_ГрафикиРаботы
		|ГДЕ
		|	ВКМ_ГрафикиРаботы.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент
		|ПОМЕСТИТЬ ВТ_Договора
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И ДоговорыКонтрагентов.ВКМ_ДействуетС <= &ДатаНачала
		|	И ДоговорыКонтрагентов.ВКМ_ДействуетПо >= &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_МесяцаВПериоде.МесяцВПериоде КАК МесяцВПериоде,
		|	ВТ_Договора.Договор КАК Договор,
		|	ВТ_Договора.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ ВТ_ДоговораИМесяца
		|ИЗ
		|	ВТ_МесяцаВПериоде КАК ВТ_МесяцаВПериоде,
		|	ВТ_Договора КАК ВТ_Договора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Реализация,
		|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ) КАК Месяц,
		|	РеализацияТоваровУслуг.Договор КАК Договор
		|ПОМЕСТИТЬ ВТ_ВсеРеализацииВПериоде
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РеализацияТоваровУслуг.Договор.ВидДоговора = &ВидДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДоговораИМесяца.Договор КАК Договор,
		|	ВТ_ДоговораИМесяца.МесяцВПериоде КАК МесяцВПериоде,
		|	ВТ_ВсеРеализацииВПериоде.Реализация КАК Реализация,
		|	ВТ_ДоговораИМесяца.Контрагент КАК Контрагент
		|ИЗ
		|	ВТ_ДоговораИМесяца КАК ВТ_ДоговораИМесяца
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВсеРеализацииВПериоде КАК ВТ_ВсеРеализацииВПериоде
		|		ПО ВТ_ДоговораИМесяца.МесяцВПериоде = ВТ_ВсеРеализацииВПериоде.Месяц
		|			И ВТ_ДоговораИМесяца.Договор = ВТ_ВсеРеализацииВПериоде.Договор";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	ТекСтрокаВыборки = 0;
	
	Пока Выборка.Следующий() И ТекСтрокаВыборки < Выборка.Количество() Цикл
		
		ТекСтрокаВыборки = ТекСтрокаВыборки + 1;
		
		Если Не ЗначениеЗаполнено(Выборка.Реализация) Тогда
			ДокументРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ДокументРеализация.Дата = Выборка.МесяцВПериоде; 
			ДокументРеализация.Контрагент = Выборка.Контрагент;
			ДокументРеализация.Комментарий = "Документ создан автоматической обработкой";
			ДокументРеализация.Организация = Объект.Организация;
			ДокументРеализация.Договор = Выборка.Договор;
			ДокументРеализация.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			ДокументРеализация.ВыполнитьАвтозаполнение();
			ДокументРеализация.ПроверитьЗаполнение();
			ДокументРеализация.Записать(РежимЗаписиДокумента.Проведение);  	
		КонецЕсли;
	КонецЦикла;
	

КонецПроцедуры
